(ns components-test.main
  (:require
   ["package:flutter/material.dart" :as m]
   ["package:mongol/mongol.dart" :as mgl]
   ["package:flutter_styled_toast/flutter_styled_toast.dart" :as st]
   [cljd.flutter :as f]
   [components.bottom-sheet :as bottom-sheet]
   [components.card :as card]
   [components.chip :as chip]
   [components.expansion-tile :as expansion-tile]
   [components.input-dialog :as input-dialog]
   [components.search-bar :as search-bar]
   [components.select :as select]
   [components.tab-layout :as tab-layout]
   [components.text-field :as text-field]
   [components.toast :as toast]
;;    [components.tooltip :as tooltip]
   ))

;; Component list for the home page
(def component-list
  [{:name "Bottom Sheet" :key "bottom_sheet" :icon m/Icons.call_to_action}
   {:name "Card" :key "card" :icon m/Icons.card_membership}
   {:name "Chip" :key "chip" :icon m/Icons.label}
   {:name "Expansion Tile" :key "expansion_tile" :icon m/Icons.expand_more}
   {:name "Input Dialog" :key "input_dialog" :icon m/Icons.input}
   {:name "Search Bar" :key "search_bar" :icon m/Icons.search}
   {:name "Select" :key "select" :icon m/Icons.arrow_drop_down}
   {:name "Tab Layout" :key "tab_layout" :icon m/Icons.tab}
   {:name "Text Field" :key "text_field" :icon m/Icons.text_fields}
   {:name "Toast" :key "toast" :icon m/Icons.notifications}
   {:name "Tooltip" :key "tooltip" :icon m/Icons.info}])

;; Component card widget
(defn component-card [{:keys [name icon on-tap]}]
  (f/widget
   :get {{{:flds [surfaceVariant onSurfaceVariant primary]} .-colorScheme} m/Theme}
   (m/GestureDetector
    .onTap on-tap
    .child (m/Container
            .margin (m/EdgeInsets.all 8)
            .padding (m/EdgeInsets.all 16)
            .decoration (m/BoxDecoration
                        .color surfaceVariant
                        .borderRadius (m/BorderRadius.circular 12)
                        .border (m/Border.all .color primary .width 1))
            .child (m/Column
                    .mainAxisSize m/MainAxisSize.min
                    .mainAxisAlignment m/MainAxisAlignment.center
                    .children
                    [(m/Icon icon .size 32 .color primary)
                     (m/SizedBox .height 12)
                     (mgl/MongolText
                      name
                      .style (m/TextStyle
                             .fontSize 16
                             .color onSurfaceVariant
                             .fontWeight m/FontWeight.w500))])))))

;; Demo pages for each component
(defn card-demo-page []
  (f/widget
   (m/Scaffold
    .appBar (m/AppBar .title (m/Text "Card Demo"))
    .body (m/SingleChildScrollView
           .scrollDirection m/Axis.horizontal
           .child (m/Row
                   .crossAxisAlignment m/CrossAxisAlignment.start
                   .children
                   [(mgl/MongolText "Mongol Cards" .style (m/TextStyle .fontSize 20 .fontWeight m/FontWeight.bold))
                    (m/SizedBox .width 16)
                    ;; Basic card with title and content
                    (card/card
                     {:title "Baraant"
                      :content "Mongol text content here"
                      :on-tap (fn [] (println "Card tapped"))})
                    (m/SizedBox .width 16)
                    ;; Card with image on left
                    (card/card
                     {:title "Ulaanbaatar"
                      :content "Card with image on the left side"
                      :image-url "https://via.placeholder.com/120x160"
                      :image-position :left
                      :on-tap (fn [] (println "Image left card tapped"))})
                    (m/SizedBox .width 16)
                    ;; Card with image on top
                    (card/card
                     {:title "Bilguun"
                      :content "Card with image on top"
                      :image-url "https://via.placeholder.com/120x160"
                      :image-position :top
                      :on-tap (fn [] (println "Image top card tapped"))})
                    (m/SizedBox .width 16)
                    ;; Scrollable card
                    (card/card
                     {:title "Scrollable Card"
                      :content "This is a scrollable card with long content that can be scrolled horizontally"
                      :scrollable? true
                      :on-tap (fn [] (println "Scrollable card tapped"))})])))))

(defn chip-demo-page []
  (let [chip-state (atom {})
        chip1 (chip/filter-chip "chip1" "Baraant" chip-state)
        chip2 (chip/filter-chip "chip2" "Ulaanbaatar" chip-state)
        chip3 (chip/filter-chip "chip3" "Bilguun" chip-state)]
    (f/widget
     (m/Scaffold
      .appBar (m/AppBar .title (m/Text "Chip Demo"))
      .body (m/Padding
             .padding (m/EdgeInsets.all 16)
             .child
             (m/Row
              .crossAxisAlignment m/CrossAxisAlignment.start
              .children
              [(mgl/MongolText "Filter Chips" .style (m/TextStyle .fontSize 20 .fontWeight m/FontWeight.bold))
               (m/SizedBox .width 16)
               (m/Row
                .children
                [chip1 chip2 chip3])]))))))

(defn expansion-tile-demo-page []
  (f/widget
   (m/Scaffold
    .appBar (m/AppBar .title (m/Text "Expansion Tile Demo"))
    .body (m/Padding
           .padding (m/EdgeInsets.all 16)
           .child
           (m/SingleChildScrollView
            .scrollDirection m/Axis.horizontal
            .child
            (m/Row
             .crossAxisAlignment m/CrossAxisAlignment.start
             .children
             [(mgl/MongolText "Expansion Tiles" .style (m/TextStyle .fontSize 20 .fontWeight m/FontWeight.bold))
              (m/SizedBox .width 16)
              (expansion-tile/expansion-tile
               {:title (mgl/MongolText "Baraant")
                :children [(mgl/MongolText "Child content 1")
                           (mgl/MongolText "Child content 2")]})
              (m/SizedBox .width 8)
              (expansion-tile/expansion-tile
               {:title (mgl/MongolText "Ulaanbaatar")
                :children [(mgl/MongolText "More content here")]})]))))))

(defn input-dialog-demo-page []
  (f/widget
   :context ctx
   :get [m/Navigator]
   :let [show-dialog (fn []
                       (m/showDialog
                        .context ctx
                        .builder (fn [_] (input-dialog/generic-input-dialog
                                          {:hint "Enter text..."
                                           :on-submit (fn [text] (println "Submitted:" text) (.pop navigator))
                                           :on-result-tap (fn [item controller atom]
                                                            (set! (.-text controller) (:text item))
                                                            (reset! atom (:text item)))}))))]
   (m/Scaffold
    .appBar (m/AppBar .title (m/Text "Input Dialog Demo"))
    .body (m/Center
           .child (m/ElevatedButton
                   .onPressed show-dialog
                   .child (m/Text "Open Input Dialog"))))))

(defn search-bar-demo-page []
  (let [search-state (atom "")]
    (f/widget
     (m/Scaffold
      .appBar (m/AppBar .title (m/Text "Search Bar Demo"))
      .body (m/Padding
             .padding (m/EdgeInsets.all 16)
             .child
             (m/Row
              .crossAxisAlignment m/CrossAxisAlignment.start
              .children
              [(mgl/MongolText "Search Bar" .style (m/TextStyle .fontSize 20 .fontWeight m/FontWeight.bold))
               (m/SizedBox .width 16)
               (search-bar/fake-search-bar
                {:state-atom search-state
                 :on-tap (fn [] (println "Search tapped"))})]))))))

(defn select-demo-page []
  (f/widget
   :let [selected-value (atom "option1")
         items [{:value "option1" :label (mgl/MongolText "Baraant")}
                {:value "option2" :label (mgl/MongolText "Ulaanbaatar")}
                {:value "option3" :label (mgl/MongolText "Bilguun")}]]

   (m/Scaffold
    .appBar (m/AppBar .title (m/Text "Select Demo"))
    .body (m/Padding
           .padding (m/EdgeInsets.all 16)
           .child
           (m/Row
            .crossAxisAlignment m/CrossAxisAlignment.start
            .children
            [(mgl/MongolText "Mongol Select" .style (m/TextStyle .fontSize 20 .fontWeight m/FontWeight.bold))
             (m/SizedBox .width 16)
             (select/mongol-select
              {:value @selected-value
               :items items
               :on-changed (fn [v] (reset! selected-value v))
               :value-builder (fn [v]
                               (let [selected-label (->> items
                                                         (filter #(= (:value %) v))
                                                         first
                                                         :label)]
                                 (m/Container
                                  .padding (m/EdgeInsets.all 12)
                                  .decoration (m/BoxDecoration
                                              .border (m/Border.all)
                                              .borderRadius (m/BorderRadius.circular 8))
                                  .child selected-label)))})])))))

(defn tab-layout-demo-page []
  (f/widget
   (m/Scaffold
    .appBar (m/AppBar .title (m/Text "Tab Layout Demo"))
    .body (tab-layout/sidebar-page
           {:tabs [{:text "Nir" :icon m/Icons.home}
                   {:text "Angilal" :icon m/Icons.category}
                   {:text "Bilguun" :icon m/Icons.person}]
            :contents [(m/Center .child (m/Text "Home Page"))
                       (m/Center .child (m/Text "Category Page"))
                       (m/Center .child (m/Text "Profile Page"))]
            :on-change (fn [idx] (println "Switched to tab:" idx))}))))

(defn text-field-demo-page []
  (let [controller1 (m/TextEditingController.)
        controller2 (m/TextEditingController.)
        controller3 (m/TextEditingController.)]
    (f/widget
     (m/Scaffold
      .appBar (m/AppBar .title (m/Text "Text Field Demo"))
      .body (m/Padding
             .padding (m/EdgeInsets.all 16)
             .child
             (m/Row
              .crossAxisAlignment m/CrossAxisAlignment.start
              .children
              [(mgl/MongolText "Text Fields" .style (m/TextStyle .fontSize 20 .fontWeight m/FontWeight.bold))
               (m/SizedBox .width 24)
               (mgl/MongolText "Basic Text Field with Hint" .style (m/TextStyle .fontSize 16 .fontWeight m/FontWeight.w500))
               (m/SizedBox .width 8)
               (text-field/text-field
                {:controller controller1
                 :hint "Naran Bichig Bichig Bichig"
                 :on-changed (fn [text] (println "Text changed:" text))})
               (m/SizedBox .width 24)
               (mgl/MongolText "Multi-line Text Field" .style (m/TextStyle .fontSize 16 .fontWeight m/FontWeight.w500))
               (m/SizedBox .width 8)
               (text-field/text-field
                {:controller controller2
                 :hint "Mongol Usughel"
                 :max-lines 3
                 :on-changed (fn [text] (println "Multi-line text changed:" text))})
               (m/SizedBox .width 24)
               (mgl/MongolText "Autofocus Text Field" .style (m/TextStyle .fontSize 16 .fontWeight m/FontWeight.w500))
               (m/SizedBox .width 8)
               (text-field/text-field
                {:controller controller3
                 :hint "Aatumaatal Altumaatal Altumaatal"
                 :autofocus true
                 :on-changed (fn [text] (println "Autofocus text changed:" text))})]))))))

(defn toast-demo-page []
  (f/widget
   :context ctx
   (m/Scaffold
    .appBar (m/AppBar .title (m/Text "Toast Demo"))
    .body (m/Padding
           .padding (m/EdgeInsets.all 16)
           .child
           (m/SingleChildScrollView 
            .child
           (m/Column
            .mainAxisAlignment m/MainAxisAlignment.center
            .crossAxisAlignment m/CrossAxisAlignment.center
            .children
            [(mgl/MongolText "Toast Demo"
                    .style (m/TextStyle .fontSize 24 .fontWeight m/FontWeight.bold))
             (m/SizedBox .height 32)
             (m/ElevatedButton
              .onPressed (fn [] (toast/success ctx "Amjilan Amjilan Asharan!"))
              .child (m/Text "Success Toast"))
             (m/SizedBox .height 16)
             (m/ElevatedButton
              .onPressed (fn [] (toast/error ctx "Aldar Bayar!"))
              .child (m/Text "Error Toast"))
             (m/SizedBox .height 16)
             (m/ElevatedButton
              .onPressed (fn [] (toast/warn ctx "Aharhal Bayar!"))
              .child (m/Text "Warning Toast"))
             (m/SizedBox .height 16)
             (m/ElevatedButton
              .onPressed (fn [] (toast/info ctx "Medegdel Bayar!"))
              .child (m/Text "Info Toast"))
             (m/SizedBox .height 32)
             (mgl/MongolText "Custom Options"
                    .style (m/TextStyle .fontSize 20 .fontWeight m/FontWeight.bold))
             (m/SizedBox .height 16)
             (m/ElevatedButton
              .onPressed (fn [] (toast/show ctx "Baraant Bayar" 
                                           :type :success 
                                           :pos st/StyledToastPosition.top
                                           :anim :rotate
                                           :dur 5))
              .child (m/Text "Top Rotate Animation (5s)"))
             (m/SizedBox .height 16)
             (m/ElevatedButton
              .onPressed (fn [] (toast/show ctx "Ulaanbaatar Bayar"
                                           :type :info
                                           :pos st/StyledToastPosition.bottom
                                           :anim :scale
                                           :dur 2))
              .child (m/Text "Bottom Scale Animation (2s)"))
             (m/SizedBox .height 16)
             (m/ElevatedButton
              .onPressed (fn [] (toast/show ctx "Bilguun Bayar"
                                           :type :warning
                                           :pos st/StyledToastPosition.center
                                           :anim :fade
                                           :manual-close? true))
              .child (m/Text "Manual Close Toast"))]))))))


;; (defn tooltip-demo-page []
;;   (f/widget
;;    (m/Scaffold
;;     .appBar (m/AppBar .title (m/Text "Tooltip Demo"))
;;     .body (m/Padding
;;            .padding (m/EdgeInsets.all 16)
;;            .child
;;            (m/Row
;;             .crossAxisAlignment m/CrossAxisAlignment.start
;;             .mainAxisAlignment m/MainAxisAlignment.center
;;             .children
;;             [(mgl/MongolText "Long press items to see tooltips"
;;                     .style (m/TextStyle .fontSize 20 .fontWeight m/FontWeight.bold))
;;              (m/SizedBox .width 32)
;;              (tooltip/v-overlay-tooltip
;;               {:message "Mongol Click Tooltip"
;;                :haptic? true
;;                :child (m/Container
;;                        .padding (m/EdgeInsets.all 16)
;;                        .decoration (m/BoxDecoration
;;                                    .color m/Colors.blue
;;                                    .borderRadius (m/BorderRadius.circular 8))
;;                        .child (m/Text "Long press me" .style (m/TextStyle .color m/Colors.white)))})
;;              (m/SizedBox .width 16)
;;              (tooltip/v-overlay-tooltip
;;               {:message "This is another tooltip"
;;                :child (m/Container
;;                        .padding (m/EdgeInsets.all 16)
;;                        .decoration (m/BoxDecoration
;;                                    .color m/Colors.green
;;                                    .borderRadius (m/BorderRadius.circular 8))
;;                        .child (m/Text "Or me" .style (m/TextStyle .color m/Colors.white)))})])))))

(defn bottom-sheet-demo-page []
  (f/widget
   :context ctx
   :let [show-bottom-sheet (fn []
                            (bottom-sheet/bottom-sheet
                             ctx
                             [{:type :header :title "Actions"}
                              {:type :action :title "Baraant" :icon m/Icons.home :on-tap (fn [] (println "Home tapped"))}
                              {:type :action :title "Ulaanbaatar" :icon m/Icons.category :on-tap (fn [] (println "Category tapped"))}
                              {:type :header :title "More"}
                              {:type :action :title "Bilguun" :icon m/Icons.person :on-tap (fn [] (println "Profile tapped"))}
                              {:type :action :title "Settings" :icon m/Icons.settings :on-tap (fn [] (println "Settings tapped"))}]
                             :height-factor 0.5
                             :show-handle? true
                             :layout-mode :scroll))]
   (m/Scaffold
    .appBar (m/AppBar .title (m/Text "Bottom Sheet Demo"))
    .body (m/Center
           .child (m/Column
                   .mainAxisAlignment m/MainAxisAlignment.center
                   .children
                   [(mgl/MongolText "Bottom Sheet Demo"
                           .style (m/TextStyle .fontSize 20 .fontWeight m/FontWeight.bold))
                    (m/SizedBox .height 32)
                    (m/ElevatedButton
                     .onPressed show-bottom-sheet
                     .child (m/Text "Show Bottom Sheet"))
                    (m/SizedBox .height 16)
                    (m/ElevatedButton
                     .onPressed (fn []
                                 (bottom-sheet/bottom-sheet
                                  ctx
                                  [{:type :action :title "Grid Item 1" :icon m/Icons.star :on-tap (fn [] (println "Item 1"))}
                                   {:type :action :title "Grid Item 2" :icon m/Icons.favorite :on-tap (fn [] (println "Item 2"))}
                                   {:type :action :title "Grid Item 3" :icon m/Icons.bookmark :on-tap (fn [] (println "Item 3"))}
                                   {:type :action :title "Grid Item 4" :icon m/Icons.share :on-tap (fn [] (println "Item 4"))}]
                                  :layout-mode :grid))
                     .child (m/Text "Show Grid Bottom Sheet"))])))))

;; Route generator
(defn get-demo-page [component-key]
  (case component-key
    "bottom_sheet" bottom-sheet-demo-page
    "card" card-demo-page
    "chip" chip-demo-page
    "expansion_tile" expansion-tile-demo-page
    "input_dialog" input-dialog-demo-page
    "search_bar" search-bar-demo-page
    "select" select-demo-page
    "tab_layout" tab-layout-demo-page
    "text_field" text-field-demo-page
    "toast" toast-demo-page
;;     "tooltip" tooltip-demo-page
    (fn [] (m/Scaffold .appBar (m/AppBar .title (m/Text "Unknown")) .body (m/Text "Unknown component")))))

;; Home page
(defn home-page []
  (f/widget
   :context ctx
   (m/Scaffold
    .appBar (m/AppBar
             .title (m/Text "MGL Components")
             .elevation 0)
    .body (m/SingleChildScrollView
           .scrollDirection m/Axis.horizontal
           .child (m/Row
                   .children
                   (map (fn [{:keys [name key icon]}]
                          (component-card
                           {:name name
                            :icon icon
                            :on-tap (fn []
                                     (.push (m/Navigator.of ctx)
                                            (m/MaterialPageRoute
                                             .builder (fn [_] ((get-demo-page key))))))}))
                        component-list))))))

(defn main []
  (m.WidgetsFlutterBinding/ensureInitialized)
  
  (f/run
   (f/widget
    (m/MaterialApp
     .title "MGL Components Test"
     .debugShowCheckedModeBanner false
     .theme (m/ThemeData
             .brightness m.Brightness/dark
             .useMaterial3 true)
     .home (home-page)
     ;(m/Center .child (m/Text "Hello, World!"))
     ))))
